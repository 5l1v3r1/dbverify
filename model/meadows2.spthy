/*
		Implementation of Meadows et al. protocol
		Version: F(...) := <nv, h(np, P)>

		By Jorge Toro 
*/

theory meadows2

begin

builtins : symmetric-encryption, asymmetric-encryption

functions: h/2

// -------------------- Protocol Rules -------------------- //
rule Verifier1:	
		[ Fr(~nv) ]
	--[ Start(~nv) ]->
		[ Out(<$V, 'hello'>),
			VerifierState1($V, ~nv) ]

rule Prover1:
		[ Fr(~np),
			In(<V, 'hello'> ) ]
	--[ Start(~np),
			Action($P) ]->
		[ ProverState1(V, $P, ~np) ]

rule Verifier2:			//fast phase starts
		[ VerifierState1(V, nv) ]
	--[ Send(V, nv) ]->
		[ Out(nv),
			VerifierState2(V, nv) ]

rule Prover2:			
		[ ProverState1(V, P, np),
			In(nv) ]
	--[ Action(P) ]->
		[ Send(P, <nv, h(np, P)>),
			ProverState2(V, P, np, nv) ]

rule Verifier3:			//fast phase ends
		[ VerifierState2(V, nv),
			Recv(V, <nv, msg>) ]
	-->
		[ VerifierState3(V, nv, msg) ]

rule Prover3:
		let s = <P, 'locP', np, nv>
				final_msg = <s, h(k, s)>
		in
		[ ProverState2(V, P, np, nv),
			//Fr(~locP),
			!Shk(V, P, k) ]
	--[ Action(P),
			End(np) ]->
		[ Send(P, final_msg) ]

rule Verifier4:
		let s = <P, locP, np, nv>
				rp = <nv, h(np, P)>	          
		in
		[ VerifierState3(V, nv, h(np, P)),
			In(<s, h(k, s)>),
			!Shk(V, P, k) ]
	--[ DBSec(V, P, nv, rp),
			End(nv) ]->
		[ ]

// -------------------- Reachability Lemmas ------------------ //
lemma complete_trace_exists:
	exists-trace
	"
	Ex V P nv np #t1 #t2 #t3 #t4 #t5 #t6 #t7.
		
		//initial phase
		Start(nv) @ t1 &
		Start(np) @ t2 &
		Action(P) @ t2 &
		
		//fast phase
		Send(V, nv) @ t3 &
		Action(P) @ t4 &
		Recv(V, <nv, h(np, P)>) @ t5 &

		//verification phase
		Action(P) @ t6 &	
		DBSec(V, P, nv, <nv, h(np, P)>) @ t7 &

		//check order
		t1 < t2 & t2 < t3 & t3 < t4 & t4 < t5 & t5 < t6 & t6 < t7
	"

//GENERIC CODE AFTER THIS LINE
//comment out whatever not needed

// ----------- Key Generation and Key Compromise -------- //
rule LtkGen:
	[ Fr(~k) ]--[ Once($X) ]->[	!Ltk($X, ~k), !Pk($X, pk(~k)), Out(pk(~k)) ]

rule ShkGen:
	[ Fr(~k) ]--[ Once(<$X, $Y>) ]->[ !Shk($X, $Y, ~k) ]

rule LtkReveal:
	[ !Ltk($X, k) ]--[ !KeyComp($X) ]->[ Out(k), !KeyComp($X) ]

rule ShkRevealX:
	[ !Shk($X, $Y, k) ]--[ !KeyComp($X) ]->[ Out(k), !KeyComp($X) ]

rule ShkRevealY:
	[ !Shk($X, $Y, k) ]--[ !KeyComp($Y) ]->[ Out(k), !KeyComp($Y) ]

// -------------- DB Network and Adversary  -------------//
rule DBAdv:
	[ In(m), !KeyComp($X) ]--[ Action($X) ]->[ Send($X, m) ]

rule DBNet:
	[ Send($X, m) ]--[ Action($Y), Recv($Y, m) ]->[ Recv($Y, m), Out(m) ]

// --------------- Sanity check & Restrictions ------------- //

// Each run id is unique
lemma unique_runid:
	"
	All x #i #j. (Start(x)@i & Start(x)@j) ==> #i=#j
	"

// Each run ends after it starts
lemma end_after_start:
	"
	All x #j. End(x)@j ==> Ex #i. Start(x)@i & (#i=#j|i<j)
	"

// Only traces where Once(x) happened once
restriction once:
	"
	All x #i #j. (Once(x)@i & Once(x)@j) ==> #i=#j
	"

/*
// Runs can always be completed
restriction completion:
	"
	All run #i. Start(run)@i ==> Ex #j. End(run)@j
	"
*/


// For each DBSec claim there was a fast phase before it
// *	Should this lemma fail, then the results for 
//		the security lemmas might be wrong
lemma no_claim_before_fastphase:
	"
	All V P chal resp #l. DBSec(V, P, chal, resp)@l ==>
		(Ex #i #j. Send(V, chal)@i & Recv(V, resp)@j & i<l & j<l)	|
		(Ex #j. !KeyComp(V)@j)		
	"


// -------------------- Security Lemmas ------------------------ //

// For each trace with a DBSec claim, it is the case
// that either the verifier is compromised, or the prover is not and he
// was active during the fast phase, or the prover is compromised and
// then there was a compromised prover active during the fast phase
lemma dbsec:
	"
	All V P chal resp #l. 
		DBSec(V, P, chal, resp)@l ==>
			(	//EITHER P is active in fast phase
				Ex #i #j #k.
					Send(V, chal)@i &
					Action(P)@j &
					Recv(V, resp)@k &
					i<j & j<k
			) | 
			(	//OR (P is compromised AND there's a compromised 
				//agent active in fast phase)
				Ex P2 #p #p2 #i #j #k.
					!KeyComp(P)@p &
					!KeyComp(P2)@p2 &
					Send(V, chal)@i &
					Action(P2)@j &
					Recv(V, resp)@k &
					i<j & j<k
			) |
			(Ex #j. !KeyComp(V)@j)
	"

// dbsec provided that prover is not compromised
// *	If this lemma fails then there's a mafia fraud
lemma dbsec_on_honest_prover:
	"
	All V P chal resp #l. 
		(DBSec(V, P, chal, resp)@l & not (Ex #j. !KeyComp(P)@j)) ==>
			(Ex #i #j #k.
				Send(V, chal)@i &
				Action(P)@j &
				Recv(V, resp)@k &
				i<j & j<k
			) |
			(Ex #j. !KeyComp(V)@j)
	"

// dbsec provided that prover is compromised
// *	If this lemma fails then there's a distance fraud,
//		or a distance hijacking, or both
lemma dbsec_on_compromised_prover:
	"
	All V P chal resp #l. 
		(DBSec(V, P, chal, resp)@l & Ex #j. !KeyComp(P)@j) ==>
			(Ex P2 #p2 #i #j #k.
				!KeyComp(P2)@p2 &
				Send(V, chal)@i &
				Action(P2)@j &
				Recv(V, resp)@k &
				i<j & j<k
			) |
			(Ex #j. !KeyComp(V)@j) 
	"
end
