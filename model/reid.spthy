/* Implementation of Reid et al's protocol */
/* By Jorge Toro */

theory reid

begin

builtins: symmetric-encryption, asymmetric-encryption

functions: KDF/2, f/2

// -------------------- Protocol Rules -------------------- //
rule Ver1:
	  [ Fr(~rB) ]
	--[ Start(~rB) ]->
	  [ Out(<$B, ~rB>),
			VerifyState1($B, ~rB) ]

rule Prov1:
	  let k = KDF(s, <$A, B, ~rA, rB>)
	      c = senc{s}k
	  in
	  [ Fr(~rA),
			In(<B, rB>),
			!Shk($A, B, s) ]
	--[ Start(~rA),
			Action($A) ]->
	  [ Send($A, <$A, ~rA>),
			ProverState1($A, B, ~rA, c, k) ]

rule Ver2:		//fast phase starts
	  [ VerifyState1(B, rB),
			Fr(~alpha),
			In(<A, rA>) ]
	--[ Send(B, ~alpha) ]->
	  [ VerifyState2(A, B, rA, rB, ~alpha),
			Out( ~alpha ) ]

rule Prov2:
	  [ ProverState1(A, B, rA, c, k),
			In(alpha) ]
	--[ Action(A),
			End(rA) ]->
	  [ Send(A, f(alpha, <c, k>) ) ]

rule Ver3:			//fast phase ends
	  let k = KDF(s, <A, B, rA, rB>)
	      c = senc{s}k
	      resp = f(alpha, <c, k>)
	  in
	  [ VerifyState2(A, B, rA, rB, alpha),
			Recv(B, resp),
			!Shk(A, B, s) ]
	--[ DBSec(B, A, alpha, resp),
			End(rB) ]->
	  [ ]

// -------------------- Reachability Lemmas ------------------ //

lemma complete_trace_exists:
	exists-trace
	"
	Ex A B s rA rB alpha #t1 #t2 #t3 #t4 #t5 #t6.
		
		//initial phase
		Start(rB) @ t1 &
		Start(rA) @ t2 &
		Action(A) @ t2 &

		//fast phase
		Send(B, alpha) @ t3 &
		Action(A) @ t4 &
		Recv(B, f(alpha, <senc{s}KDF(s, <A, B, rA, rB>), 
			KDF(s, <A, B, rA, rB>) >) ) @ t5 &

		//verification
		DBSec(B, A, alpha, f(alpha, <senc{s}KDF(s, <A, B, rA, rB>), 
			KDF(s, <A, B, rA, rB>) >)) @ t6 &

		//order check
		t1 < t2 & t2 < t3 & t3 < t4 & t4 < t5 & t5 < t6
	"

//GENERIC CODE AFTER THIS LINE
//comment out whatever not needed

// ----------- Key Generation and Key Compromise -------- //
rule LtkGen:
	[ Fr(~k) ]--[ Once($X) ]->[	!Ltk($X, ~k), !Pk($X, pk(~k)), Out(pk(~k)) ]

rule ShkGen:
	[ Fr(~k) ]--[ Once(<$X, $Y>) ]->[ !Shk($X, $Y, ~k) ]

rule LtkReveal:
	[ !Ltk($X, k) ]--[ !KeyComp($X) ]->[ Out(k), !KeyComp($X) ]

rule ShkRevealX:
	[ !Shk($X, $Y, k) ]--[ !KeyComp($X) ]->[ Out(k), !KeyComp($X) ]

rule ShkRevealY:
	[ !Shk($X, $Y, k) ]--[ !KeyComp($Y) ]->[ Out(k), !KeyComp($Y) ]

// -------------- DB Network and Adversary  -------------//
rule DBAdv:
	[ In(m), !KeyComp($X) ]--[ Action($X) ]->[ Send($X, m) ]

rule DBNet:
	[ Send($X, m) ]--[ Action($Y), Recv($Y, m) ]->[ Recv($Y, m), Out(m) ]

// --------------- Sanity check & Restrictions ------------- //

// Each run id is unique
lemma unique_runid:
	"
	All x #i #j. (Start(x)@i & Start(x)@j) ==> #i=#j
	"

// Each run ends after it starts
lemma end_after_start:
	"
	All x #j. End(x)@j ==> Ex #i. Start(x)@i & (#i=#j|i<j)
	"

// Only traces where Once(x) happened once
restriction once:
	"
	All x #i #j. (Once(x)@i & Once(x)@j) ==> #i=#j
	"

/*
// Runs can always be completed
restriction completion:
	"
	All run #i. Start(run)@i ==> Ex #j. End(run)@j
	"
*/


// For each DBSec claim there was a fast phase before it
// *	Should this lemma fail, then the results for 
//		the security lemmas might be wrong
lemma no_claim_before_fastphase:
	"
	All V P chal resp #l. DBSec(V, P, chal, resp)@l ==>
		(Ex #i #j. Send(V, chal)@i & Recv(V, resp)@j & i<l & j<l)	|
		(Ex #j. !KeyComp(V)@j)		
	"


// -------------------- Security Lemmas ------------------------ //

// For each trace with a DBSec claim, it is the case
// that either the verifier is compromised, or the prover is not and he
// was active during the fast phase, or the prover is compromised and
// then there was a compromised prover active during the fast phase
lemma dbsec:
	"
	All V P chal resp #l. 
		DBSec(V, P, chal, resp)@l ==>
			(	//EITHER P is active in fast phase
				Ex #i #j #k.
					Send(V, chal)@i &
					Action(P)@j &
					Recv(V, resp)@k &
					i<j & j<k
			) | 
			(	//OR (P is compromised AND there's a compromised 
				//agent active in fast phase)
				Ex P2 #p #p2 #i #j #k.
					!KeyComp(P)@p &
					!KeyComp(P2)@p2 &
					Send(V, chal)@i &
					Action(P2)@j &
					Recv(V, resp)@k &
					i<j & j<k
			) |
			(Ex #j. !KeyComp(V)@j)
	"

// dbsec provided that prover is not compromised
// *	If this lemma fails then there's a mafia fraud
lemma dbsec_on_honest_prover:
	"
	All V P chal resp #l. 
		(DBSec(V, P, chal, resp)@l & not (Ex #j. !KeyComp(P)@j)) ==>
			(Ex #i #j #k.
				Send(V, chal)@i &
				Action(P)@j &
				Recv(V, resp)@k &
				i<j & j<k
			) |
			(Ex #j. !KeyComp(V)@j)
	"

// dbsec provided that prover is compromised
// *	If this lemma fails then there's a distance fraud,
//		or a distance hijacking, or both
lemma dbsec_on_compromised_prover:
	"
	All V P chal resp #l. 
		(DBSec(V, P, chal, resp)@l & Ex #j. !KeyComp(P)@j) ==>
			(Ex P2 #p2 #i #j #k.
				!KeyComp(P2)@p2 &
				Send(V, chal)@i &
				Action(P2)@j &
				Recv(V, resp)@k &
				i<j & j<k
			) |
			(Ex #j. !KeyComp(V)@j) 
	"
end
