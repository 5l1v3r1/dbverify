/*
		Implementation of Brands and Chaum's protocol
		https://doi.org/10.1007/3-540-48285-7_30

		Version: Fiat-Shamir identification
*/

theory BC_FiatShamirID

begin

builtins: 	symmetric-encryption, asymmetric-encryption, xor

functions: 	sq/1, mul/2, pow/2, check/2, true/0

equations: 	check( sq(mul(pow(x, y), r)), mul(pow(pk(x), y), sq(r)) ) = true

// -------------------- Protocol Rules -------------------- //
rule Prov1:
	  let msg = <sq(~r), senc(~b, ~k)>
	  in
	  [ Fr(~r),
	  	Fr(~k),
	  	Fr(~b) ]
	--[ Start(~b) ]->
	  [ Send($P, msg),
	    ProverState1($P, ~r, ~b, ~k),
	    MayLeak($P, ~b) ]

rule Ver1:			//fast phase starts
	  [ In(<r_squared, sealed_commit>),
	    Fr(~a) ]
	--[ Start(~a),
	    Send($V, ~a) ]->
	  [ Out(~a),
	    VerifierState1($V, r_squared, sealed_commit, ~a) ]

rule Prov2:
	  [ ProverState1(P, r, b, k),
	    In(a) ]
	-->
	  [ Send(P, b),
	    ProverState2(P, r, b, k, a) ]

rule Ver2:			//fast phase ends
	  [ VerifierState1(V, r_squared, sealed_commit, a),
	    Recv (V, b) ]
	-->
	  [ VerifierState2(V, r_squared, sealed_commit, a, b) ]

rule Prov3:
	  let s = mul(pow(x, a ⊕ b), r)
	  in
	  [ ProverState2(P, r, b, k, a),
	    !Ltk(P, x) ]
	--[ End(b) ]->
	  [ Send(P, <k,s>) ]

rule Ver3:
	  let left = sq(s)
	      right = mul(pow(pkP, a ⊕ b), r_squared)
	      checkEq = check(left, right)
	  in
	  [ In(<k, s>),
	    VerifierState2(V, r_squared, senc(b, k), a, b),
	    !Pk( $P, pkP ) ]
	--[ Eq(checkEq, true),
	    DBSec(V, $P, a, b),
	    End(a) ]->
	  [ ]
// -------------------- Axioms ------------------------------- //
restriction equals:
    "
    All a b #i. Eq(a,b)@i ==> a=b
    "
//GENERIC CODE AFTER THIS LINE
///////////////////////////////////////////////////////////////////////

// ------------------- Key Generation and Compromise --------------- //
rule LtkGen:
	[ Fr(~k) ]--[ Once($X) ]->[	!Ltk($X, ~k), !Pk($X, pk(~k)), Out(pk(~k)) ]

rule ShkGen:
	[ Fr(~k) ]--[ Once(<$X, $Y>) ]->[ !Shk($X, $Y, ~k) ]

rule LtkReveal:
	[ !Ltk($X, k) ]--[ !Compromise($X) ]->[ Out(k), !Compromise($X) ]

rule ShkRevealX:
	[ !Shk($X, $Y, k) ]--[ !Compromise($X) ]->[ Out(k), !Compromise($X) ]

rule ShkRevealY:
	[ !Shk($X, $Y, k) ]--[ !Compromise($Y) ]->[ Out(k), !Compromise($Y) ]

// ------------------- Network and Adversary  ----------------------- //
rule DBInject:
	[ In(m), !Compromise($X) ]-->[ Send($X, m) ]

rule DBSend:
	[ Send($X, m) ]--[ Send($X, m), Action($X) ]->[ Net(m), Out(m) ]

rule DBRecv:
	[ Net(m) ]--[ Action($Y), Recv($Y, m) ]->[ Recv($Y, m) ]

// -------------------- Collusion (Leakage) Rule -------------------- //
rule Leak:
	[ MayLeak($X, m) ]--[ Action($X), Collusion() ]->[ Out(m) ]

// ------------------- Reachability & Sanity Check ------------------ //
// Clean trace exists
lemma reachability:
	exists-trace
	"
	Ex V P chal resp #i /*#i2*/ #j #k #l.
		Send(V, chal)@i &
		//Recv(P, chal)@i2 &
		Send(P, resp)@j &
		Recv(V, resp)@k &
		DBSec(V, P, chal, resp)@l &
		i<j & j<k & k<l &
		not (Ex C #r. !Compromise(C)@r) &
		not (Ex #c. Collusion()@c)
	"

// Each run id is unique
lemma unique_runid:
	"All x #i #j. (Start(x)@i & Start(x)@j) ==> #i=#j"

// Each run ends after it starts
lemma end_after_start:
	"All x #j. End(x)@j ==> Ex #i. Start(x)@i & (#i=#j|i<j)"

// For each DBSec claim there was a fast phase before it
// *	Should this lemma fail, then the results for 
//		the security lemmas might be wrong
lemma fast_before_claim:
	"
	All V P chal resp #l. DBSec(V, P, chal, resp)@l ==>
		(Ex #i #j. Send(V, chal)@i & Recv(V, resp)@j & i<l & j<l)	|
		(Ex #j. !Compromise(V)@j)		
	"
// ------------------------ Restrictions ---------------------------- //
// Only traces where Once(x) happened once
restriction once:
	"All x #i #j. (Once(x)@i & Once(x)@j) ==> #i=#j"

/*
// Runs can always be completed
restriction completion:
	"
	All run #i. Start(run)@i ==> Ex #j. End(run)@j
	"
*/
// ----------------- Expiry Rule and Restrictions ------------------- //
rule SetExpiry:
	[ ]--[ Expiry(), Once('expiry') ]->[ ]

restriction no_collusion_after_expiry:
	"All #i #j. (Expiry()@i & Collusion()@j) ==> j<i"

restriction sessions_complete_before_expiry:
	"All x #i #e. (Start(x)@i & Expiry()@e & i<e) ==> Ex #j. (End(x)@j & j<e)"


////////////////////////////////////////////////////////////////////////
// ------------------------ Security Lemmas ------------------------- //
////////////////////////////////////////////////////////////////////////

// For each trace (in which collusion did not occurr) it is the case that
// * 	EITHER the verifier is compromised,
// * 	OR the prover is not and he was active during the fast phase,
// * 	OR the prover is compromised and there was a compromised 
//		prover active during the fast phase.
lemma dbsec:
	"
	All V P chal resp #l. 
		DBSec(V, P, chal, resp)@l ==>
			(	//EITHER P is active in fast phase
				Ex #i #j #k.
					Send(V, chal)@i &
					Action(P)@j &
					Recv(V, resp)@k &
					i<j & j<k
			) | 
			(	//OR (P is compromised AND there's a compromised 
				//agent active in fast phase)
				Ex P2 #p #p2 #i #j #k.
					!Compromise(P)@p &
					!Compromise(P2)@p2 &
					Send(V, chal)@i &
					Action(P2)@j &
					Recv(V, resp)@k &
					i<j & j<k
			) |
			(Ex #j. !Compromise(V)@j) |
			(Ex #j. Collusion()@j) //this is just to drop traces w. collusion
	"

// dbsec provided that neither the prover nor verifier are compromised
// * 	If this lemma fails, then there's a mafia fraud
// * 	If this lemma holds and dbsec fails, then there's a 
//		distance fraud, or a distance hijacking, or both
lemma dbsec_hnst:
	"
	All V P chal resp #l. 
		DBSec(V, P, chal, resp)@l ==>
			(Ex #i #j #k.
				Send(V, chal)@i &
				Action(P)@j &
				Recv(V, resp)@k &
				i<j & j<k
			) |
			(Ex #j. !Compromise(V)@j) |
			(Ex #j. !Compromise(P)@j) |
			(Ex #j. Collusion()@j) //this is just to drop traces w. collusion
	"

// dbsec_hnst with collusion
lemma dbsec_hnst_collusion:
	"
	All V P chal resp #l.
		DBSec(V, P, chal, resp)@l ==>
			(Ex #i #j #k.
				Send(V, chal)@i &
				Action(P)@j &
				Recv(V, resp)@k &
				i<j & j<k
			) |
			(Ex #j. !Compromise(V)@j) |
			(Ex #j. !Compromise(P)@j)
	"

// post-collusion security w.r.t dbsec_hnst
// *	If this lemma holds and dbsec_hnst_collusion fails,
//		then there's a terrorist fraud
lemma dbsec_hnst_star:
	"
	All V P chal resp #e #l.
		(DBSec(V, P, chal, resp)@l & Expiry()@e & e<l) ==>
			(Ex #i #j #k.
				Send(V, chal)@i &
				Action(P)@j &
				Recv(V, resp)@k &
				i<j & j<k
			) |
			(Ex #j. !Compromise(V)@j) |
			(Ex #j. !Compromise(P)@j)
	"
end
