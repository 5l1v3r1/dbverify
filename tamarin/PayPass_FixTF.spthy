/*
		Implementation of Mastercard's PayPass with Relay Resistance
		Version: Repaired protocol that satisfies dbsec and resists TF
*/

theory PayPass_FixTF

begin

builtins: 	symmetric-encryption, asymmetric-encryption, signing, multiset, xor

functions: 	MAC/4, f/2

// -------------------- Protocol Rules -------------------- //
rule InitCounter:
		[ ]
	--[	Once(<$R, $C, 'ATC'>) ]->
		[ //CounterInCard($C, $R, '0'),
			CounterInReader($R, $C, '0') ]

rule Card1:
		let xored = ~nC ⊕ KM
		in
		[ Fr(~nC),
			Fr(~ti),
			!Shk($R, $C, KM) ]
	--[ Start(~nC) ]->
		[ CardState1($C, $R, ~nC, xored, ~ti),
			
			//collusion-triggering fact
			//'ignored' in dbsec and dbsec_hnst lemmas
			MayLeak($C, xored) //this will reveal KM
		]

rule Reader1:				//fast phase stats
		let chal = <'ExchRelayResistData', ~UN>
		in
    [ Fr(~UN) ]
  --[ Start(~UN),

  		//log 'send fast-phase challenge' fact
  		Send($R, chal) 
  	]->
    [ ReaderState1($R, ~UN),
    	
    	//send fast-phase challenge
    	Out( chal ) ]

rule Card2:
		let chal =  <'ExchRelayResistData', UN>
				resp = <nC, ti, f(UN, xored)>
		in
    [ CardState1(C, R, nC, xored, ti),
    	In( chal ) ]
  -->
    [ CardState2(C, R, nC, ti, UN),
    	
    	//send fast-phase response
    	Send(C, resp) ]

rule Reader2:			//fast phase ends
		let resp = <nC, ti, msg>
				req = <'GenerateAC', UN, ~Amount, 'Currency'>
		in
		[ ReaderState1(R, UN),
			Fr(~Amount),
			
			//receive fast-phase response
			Recv(R, resp) ]
  -->
    [ ReaderState2(R, UN, nC, ti, msg, ~Amount),
    	
    	//send request for authentication
    	Out( req ) ]

rule Card3:
		let req = <'GenerateAC', UN, Amount, 'Currency'>
				KS = senc{ATC}KM
				AC = MAC(KS, Amount, ATC, UN)
				SDAD = sign{<UN, AC>}skC
				auth = <SDAD, AC, ATC>
		in
		[ CardState2(C, R, nC, ti, UN),
			//CounterInCard(C, $R, ATC),
			!Shk(C, R, KM),
			!Ltk(C, skC),
			In( req ),
			
			//the adversary knows the counter
			//this is trivial as they can count
			//this is to tweak partial deconstructions
			In( <'AdvKnows_ATC', ATC> )
		]
	--[ End(nC) ]->
		[ Send(C, auth)/*,
			CounterInCard(C, $R, ATC + '1')*/ ]

rule Reader3:
		let chal = <'ExchRelayResistData', UN>
				resp = <nC, ti, f(UN, nC ⊕ KM)>
				KS = senc{ATC}KM
				AC = MAC(KS, Amount, ATC, UN)
				verifSignAuth = verify(SDAD, <UN, AC>, pkC)
				auth = <SDAD, AC, ATC>
		in
		[ ReaderState2(R, UN, nC, ti, f(UN, nC ⊕ KM), Amount),
			CounterInReader(R, $C, ATC2),
			!Pk($C, pkC),
			!Shk(R, $C, KM),
			In(auth) ]
	--[ 
			//for counter synchronization
			LessOrEqual(ATC2, ATC),
			//a stronger counter synch check (but less realistic):
			//Eq(ATC2, ATC),
			
			//check signature
			Eq(verifSignAuth, true),

			//DBSec fact
			DBSec(R, $C, chal, resp),

			//end
			End(UN) ]->
		[ 
			//update counter
			CounterInReader(R, $C, ATC + '1')
		] 

restriction equal:
  "All x y #i. Eq(x, y)@i ==> x = y"

restriction less_or_equal:
  "All x y #i. LessOrEqual(x, y)@i ==> x = y | Ex z. x + z = y"

//GENERIC CODE AFTER THIS LINE
///////////////////////////////////////////////////////////////////////

// ------------------- Key Generation and Compromise --------------- //
rule LtkGen:
	[ Fr(~k) ]--[ Once($X) ]->[	!Ltk($X, ~k), !Pk($X, pk(~k)), Out(pk(~k)) ]

rule ShkGen:
	[ Fr(~k) ]--[ Once(<$X, $Y>) ]->[ !Shk($X, $Y, ~k) ]

rule LtkReveal:
	[ !Ltk($X, k) ]--[ !Compromise($X) ]->[ Out(k), !Compromise($X) ]

rule ShkRevealX:
	[ !Shk($X, $Y, k) ]--[ !Compromise($X) ]->[ Out(k), !Compromise($X) ]

rule ShkRevealY:
	[ !Shk($X, $Y, k) ]--[ !Compromise($Y) ]->[ Out(k), !Compromise($Y) ]

// ------------------- Network and Adversary  ----------------------- //
rule DBInject:
	[ In(m), !Compromise($X) ]-->[ Send($X, m) ]

rule DBSend:
	[ Send($X, m) ]--[ Send($X, m), Action($X) ]->[ Net(m), Out(m) ]

rule DBRecv:
	[ Net(m) ]--[ Action($Y), Recv($Y, m) ]->[ Recv($Y, m) ]

// -------------------- Collusion (Leakage) Rule -------------------- //
rule Leak:
	[ MayLeak($X, m) ]--[ Action($X), Collusion() ]->[ Out(m) ]

// ------------------- Reachability & Sanity Check ------------------ //
// Clean trace exists
lemma reachability:
	exists-trace
	"
	Ex V P chal resp #i /*#i2*/ #j #k #l.
		Send(V, chal)@i &
		//Recv(P, chal)@i2 &
		Send(P, resp)@j &
		Recv(V, resp)@k &
		DBSec(V, P, chal, resp)@l &
		i<j & j<k & k<l &
		not (Ex C #r. !Compromise(C)@r) &
		not (Ex #c. Collusion()@c)
	"

// Each run id is unique
lemma unique_runid:
	"All x #i #j. (Start(x)@i & Start(x)@j) ==> #i=#j"

// Each run ends after it starts
lemma end_after_start:
	"All x #j. End(x)@j ==> Ex #i. Start(x)@i & (#i=#j|i<j)"

// For each DBSec claim there was a fast phase before it
// *	Should this lemma fail, then the results for 
//		the security lemmas might be wrong
lemma fast_before_claim:
	"
	All V P chal resp #l. DBSec(V, P, chal, resp)@l ==>
		(Ex #i #j. Send(V, chal)@i & Recv(V, resp)@j & i<l & j<l)	|
		(Ex #j. !Compromise(V)@j)		
	"
// ------------------------ Restrictions ---------------------------- //
// Only traces where Once(x) happened once
restriction once:
	"All x #i #j. (Once(x)@i & Once(x)@j) ==> #i=#j"

/*
// Runs can always be completed
restriction completion:
	"
	All run #i. Start(run)@i ==> Ex #j. End(run)@j
	"
*/
// ----------------- Expiry Rule and Restrictions ------------------- //
rule SetExpiry:
	[ ]--[ Expiry(), Once('expiry') ]->[ ]

restriction no_collusion_after_expiry:
	"All #i #j. (Expiry()@i & Collusion()@j) ==> j<i"

restriction sessions_complete_before_expiry:
	"All x #i #e. (Start(x)@i & Expiry()@e & i<e) ==> Ex #j. (End(x)@j & j<e)"


////////////////////////////////////////////////////////////////////////
// ------------------------ Security Lemmas ------------------------- //
////////////////////////////////////////////////////////////////////////

// For each trace (in which collusion did not occurr) it is the case that
// * 	EITHER the verifier is compromised,
// * 	OR the prover is not and he was active during the fast phase,
// * 	OR the prover is compromised and there was a compromised 
//		prover active during the fast phase.
lemma dbsec:
	"
	All V P chal resp #l. 
		DBSec(V, P, chal, resp)@l ==>
			(	//EITHER P is active in fast phase
				Ex #i #j #k.
					Send(V, chal)@i &
					Action(P)@j &
					Recv(V, resp)@k &
					i<j & j<k
			) | 
			(	//OR (P is compromised AND there's a compromised 
				//agent active in fast phase)
				Ex P2 #p #p2 #i #j #k.
					!Compromise(P)@p &
					!Compromise(P2)@p2 &
					Send(V, chal)@i &
					Action(P2)@j &
					Recv(V, resp)@k &
					i<j & j<k
			) |
			(Ex #j. !Compromise(V)@j) |
			(Ex #j. Collusion()@j) //this is just to drop traces w. collusion
	"

// dbsec provided that neither the prover nor verifier are compromised
// * 	If this lemma fails, then there's a mafia fraud
// * 	If this lemma holds and dbsec fails, then there's a 
//		distance fraud, or a distance hijacking, or both
lemma dbsec_hnst:
	"
	All V P chal resp #l. 
		DBSec(V, P, chal, resp)@l ==>
			(Ex #i #j #k.
				Send(V, chal)@i &
				Action(P)@j &
				Recv(V, resp)@k &
				i<j & j<k
			) |
			(Ex #j. !Compromise(V)@j) |
			(Ex #j. !Compromise(P)@j) |
			(Ex #j. Collusion()@j) //this is just to drop traces w. collusion
	"

// dbsec_hnst with collusion
lemma dbsec_hnst_collusion:
	"
	All V P chal resp #l.
		DBSec(V, P, chal, resp)@l ==>
			(Ex #i #j #k.
				Send(V, chal)@i &
				Action(P)@j &
				Recv(V, resp)@k &
				i<j & j<k
			) |
			(Ex #j. !Compromise(V)@j) |
			(Ex #j. !Compromise(P)@j)
	"

// post-collusion security w.r.t dbsec_hnst
// *	If this lemma holds and dbsec_hnst_collusion fails,
//		then there's a terrorist fraud
lemma dbsec_hnst_star:
	"
	All V P chal resp #e #l.
		(DBSec(V, P, chal, resp)@l & Expiry()@e & e<l) ==>
			(Ex #i #j #k.
				Send(V, chal)@i &
				Action(P)@j &
				Recv(V, resp)@k &
				i<j & j<k
			) |
			(Ex #j. !Compromise(V)@j) |
			(Ex #j. !Compromise(P)@j)
	"
end
